import java.text.SimpleDateFormat

apply plugin: 'maven-publish'
apply plugin: 'java'

repositories {
    mavenCentral()
}

group = 'eu.teamvirtual'
archivesBaseName = 'launchwrapper'
version = '2.2'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

def ENV = System.getenv()

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            //Maybe there is a better solution to this but it will do for now.
            if(ENV.MAVEN_PASSWORD) {
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            } else {
                credentials {
                    username "$teamvirtual_maven_user"
                    password "$teamvirtual_maven_pw"
                }
            }
            url = "https://maven.teamvirtual.eu/repository/maven-releases/"
        }
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Version' : project.version,
            'Implementation-Vendor' : "VirtualClient",
            'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Created-By'     : "Gradle ${gradle.gradleVersion}"
        )
    }
}